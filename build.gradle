buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
    maven {
        url "http://www.beyondrealitypack.com/maven2/"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
}

apply plugin: 'forge'
apply plugin: 'curseforge'

ext.configFile = file "build.properties"

String APIToken = ''
if (file('APIToken').exists()) {
    APIToken = new File('APIToken').text
}

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

String build = System.getenv("BUILD_NUMBER")
if (build == null || build == '') {
    version = "${config.minecraft_version}-${config.mod_version}"
} else {
    version = "${config.minecraft_version}-${config.mod_version}-B" + System.getenv("BUILD_NUMBER")
}

group= "com.teamcos.modularsystems" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ModularSystems"
sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    version = config.minecraft_version + "-" + config.forge_version + "-" + config.minecraft_version

    replaceIn "core/lib/Reference.java"
    replace "@VERSION@", "${config.mod_version}"

    runDir = "eclipse"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile "codechicken:CodeChickenCore:${config.minecraft_version}-${config.ccc_version}:dev"
    compile "codechicken:CodeChickenLib:${config.minecraft_version}-${config.CCLIB_version}:dev"
    compile "codechicken:NotEnoughItems:${config.minecraft_version}-${config.NEI_version}:dev"
    compile "mcp.mobius.waila:Waila:${config.WAILA_version}_${config.minecraft_version}:dev"
    compile "net.industrial-craft:industrialcraft-2:${config.IC2_version}-experimental:dev"
    compile group: "com.teambr.bookshelf", name: "Bookshelf", version: "+", classifier: "dev", changing: true

}

processResources {
    // exclude xcf files, as they are for development only
    exclude '**/*.xcf'

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'

        // replace version and mcversion
        expand 'mod_version': project.version, 'minecraft_version': project.config.minecraft_version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

curse {
    projectId = '68528'
    apiKey = APIToken
    changelog = 'https://github.com/TeamCoS/Modular-Systems/wiki/Changelog'
    releaseType = 'release'
    addGameVersion '1.7.10'
    relatedProject 'bookshelf-api-library'
}
